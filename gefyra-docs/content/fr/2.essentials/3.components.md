---
title: Composants Markdown
description: Utilisez les composants Markdown pour structurer votre contenu, avec l'aide de Nuxt UI Pro.
navigation:
  icon: i-lucide-component
  title: Composants
---

Les composants prose sont des remplacements pour les balises de typographie HTML. Ils offrent un moyen simple de personnaliser votre interface lors de l'utilisation de Markdown.

**Docus et Nuxt UI Pro** fournissent un ensemble de composants prose stylés et élégants pour vous aider à rédiger votre documentation avec la [syntaxe MDC](https://content.nuxt.com/docs/files/markdown#mdc-syntax).

::prose-note{to="https://ui.nuxt.com/getting-started"}
Cette page met en avant uniquement les composants prose les plus adaptés à la rédaction de documentation. Cependant, vous pouvez utiliser **n'importe quel composant Nuxt UI ou Nuxt UI Pro** dans votre Markdown. Pour la liste complète des composants disponibles, consultez la documentation Nuxt UI.
::

### `Accordion`

Utilisez les composants `accordion` et `accordion-item` pour afficher un [Accordion](https://ui.nuxt.com/components/accordion) dans votre contenu.

::tabs
  :::tabs-item{icon="i-lucide-eye" label="Aperçu"}
    ::::accordion
      :::::accordion-item
      ---
      icon: i-lucide-circle-help
      label: Qu'est-ce que Docus et quelles sont ses fonctionnalités clés ?
      ---
      Docus est une solution de documentation entièrement intégrée construite avec Nuxt UI Pro. C'est un thème basé sur le template de documentation UI Pro qui fournit un visuel prêt à l'emploi. L'utilisateur peut se concentrer sur le contenu en utilisant Markdown et la syntaxe MDC.
      :::::
    
      :::::accordion-item
      ---
      icon: i-lucide-circle-help
      label: Comment démarrer avec Docus ?
      ---
      La seule chose dont vous avez besoin pour démarrer un projet Docus est un dossier `content/`. Consultez le starter pour un démarrage rapide.
      :::::
    
      :::::accordion-item{icon="i-lucide-circle-help" label="Qu'est-ce que Nuxt UI Pro ?"}
      Nuxt UI Pro est une collection de composants Vue premium, de composables et d'utilitaires construits sur [Nuxt UI](https://ui.nuxt.com/). Nuxt UI Pro est gratuit en développement, mais nécessite une licence pour la production.
      :::::
    ::::
  :::

  :::tabs-item{icon="i-lucide-code" label="Code"}
  ```mdc
  ::accordion
  
    :::accordion-item{label="Qu'est-ce que Docus et quelles sont ses fonctionnalités clés ?" icon="i-lucide-circle-help"}
    Docus est une solution de documentation entièrement intégrée construite avec Nuxt UI Pro. C'est un thème basé sur le template de documentation UI Pro qui fournit un visuel prêt à l'emploi. L'utilisateur peut se concentrer sur le contenu en utilisant Markdown et la syntaxe MDC.
    :::
  
    :::accordion-item{label="Comment démarrer avec Docus ?" icon="i-lucide-circle-help"}
    La seule chose dont vous avez besoin pour démarrer un projet Docus est un dossier `content/`. Consultez le starter pour un démarrage rapide.
    :::
  
    :::accordion-item{label="Qu'est-ce que Nuxt UI Pro ?" icon="i-lucide-circle-help"}
    Nuxt UI Pro est une collection de composants Vue premium, de composables et d'utilitaires construits sur [Nuxt UI](https://ui.nuxt.com/). Nuxt UI Pro est gratuit en développement, mais nécessite une licence pour la production.
    :::
  ::
  ```
  :::
::

### `Badge`

Utilisez du markdown dans le slot par défaut du composant `badge` pour afficher un [Badge](https://ui.nuxt.com/components/badge) dans votre contenu.

::tabs
  :::tabs-item{.my-5 icon="i-lucide-eye" label="Aperçu"}
    ::::badge
    **v3.0.0**
    ::::
  :::

  :::tabs-item{icon="i-lucide-code" label="Code"}
  ```mdc
  ::badge
  **v3.0.0**
  ::
  ```
  :::
::

### `Callout`

Utilisez du markdown dans le slot par défaut du composant `callout` pour ajouter un contexte visuel à votre contenu.

Utilisez les props `icon` et `color` pour le personnaliser. Vous pouvez aussi passer n'importe quelle propriété du composant [`<NuxtLink>`](https://nuxt.com/docs/api/components/nuxt-link).

Vous pouvez également utiliser les raccourcis `note`, `tip`, `warning` et `caution` avec des icônes et couleurs prédéfinies.

::tabs
  :::tabs-item{.my-5 icon="i-lucide-eye" label="Aperçu"}
    ::::div{.flex.flex-col.gap-4.w-full}
      :::::note{.w-full.my-0}
      Voici des informations supplémentaires pour vous.
      :::::
    
      :::::tip{.w-full.my-0}
      Voici une suggestion utile.
      :::::
    
      :::::warning{.w-full.my-0}
      Faites attention à cette action car elle pourrait avoir des résultats inattendus.
      :::::
    
      :::::caution{.w-full.my-0}
      Cette action est irréversible.
      :::::
    ::::
  :::

  :::tabs-item{icon="i-lucide-code" label="Code"}
  ```mdc
  ::note
  Voici des informations supplémentaires.
  ::
  
  ::tip
  Voici une suggestion utile.
  ::
  
  ::warning
  Faites attention à cette action car elle pourrait avoir des résultats inattendus.
  ::
  
  ::caution
  Cette action est irréversible.
  ::
  ```
  :::
::

### `Card` et `CardGroup`

Utilisez du markdown dans le slot par défaut du composant `card` pour mettre en avant votre contenu.

Utilisez les props `title`, `icon` et `color` pour le personnaliser. Vous pouvez aussi passer n'importe quelle propriété du composant [`<NuxtLink>`](https://nuxt.com/docs/api/components/nuxt-link).

Regroupez vos composants `card` avec le composant `card-group` pour les afficher en grille.

::tabs
  :::tabs-item{.my-5 icon="i-lucide-eye" label="Aperçu"}
    ::::card-group{.w-full.my-0}
      :::::card
      ---
      icon: i-simple-icons-github
      target: _blank
      title: Tableau de bord
      to: https://github.com/nuxt-ui-pro/dashboard
      ---
      Un tableau de bord avec une mise en page multi-colonnes.
      :::::
    
      :::::card
      ---
      icon: i-simple-icons-github
      target: _blank
      title: SaaS
      to: https://github.com/nuxt-ui-pro/saas
      ---
      Un template avec landing, pricing, docs et blog.
      :::::
    
      :::::card
      ---
      icon: i-simple-icons-github
      target: _blank
      title: Docs
      to: https://github.com/nuxt-ui-pro/docs
      ---
      Une documentation avec `@nuxt/content`.
      :::::
    
      :::::card
      ---
      icon: i-simple-icons-github
      target: _blank
      title: Landing
      to: https://github.com/nuxt-ui-pro/landing
      ---
      Une page d'accueil à utiliser comme point de départ.
      :::::
    ::::
  :::

  :::tabs-item{.my-5 icon="i-lucide-eye" label="Aperçu"}
  ```mdc
  :::card-group
  
    ::card
    ---
    title: Tableau de bord
    icon: i-simple-icons-github
    to: https://github.com/nuxt-ui-pro/dashboard
    target: _blank
    ---
    Un tableau de bord avec une mise en page multi-colonnes.
    ::
  
    ::card
    ---
    title: SaaS
    icon: i-simple-icons-github
    to: https://github.com/nuxt-ui-pro/saas
    target: _blank
    ---
    Un template avec landing, pricing, docs et blog.
    ::
  
    ::card
    ---
    title: Docs
    icon: i-simple-icons-github
    to: https://github.com/nuxt-ui-pro/docs
    target: _blank
    ---
    Une documentation avec `@nuxt/content`.
    ::
  
    ::card
    ---
    title: Landing
    icon: i-simple-icons-github
    to: https://github.com/nuxt-ui-pro/landing
    target: _blank
    ---
    Une page d'accueil à utiliser comme point de départ.
    ::
  
  :::
  ```
  :::
::

### `Collapsible`

Enveloppez votre contenu avec le composant `collapsible` pour afficher un [Collapsible](https://ui.nuxt.com/components/collapsible) dans votre contenu.

::tabs
  :::tabs-item{.my-5 icon="i-lucide-eye" label="Aperçu"}
    ::::collapsible
    | Prop    | Défaut    | Type     |
    | ------- | --------- | -------- |
    | `name`  |           | `string` |
    | `size`  | `md`      | `string` |
    | `color` | `neutral` | `string` |
    ::::
  :::

  :::tabs-item{icon="i-lucide-code" label="Code"}
  ```mdc
  ::collapsible
  
  | Prop    | Défaut    | Type                     |
  |---------|-----------|--------------------------|
  | `name`  |           | `string`{lang="ts-type"} |
  | `size`  | `md`      | `string`{lang="ts-type"} |
  | `color` | `neutral` | `string`{lang="ts-type"} |
  
  ::
  ```
  :::
::

### `Field` et `FieldGroup`

Un `field` est une prop ou un paramètre à afficher dans votre contenu. Vous pouvez les regrouper avec `field-group` dans une liste.

::tabs
  :::tabs-item{.my-5 icon="i-lucide-eye" label="Aperçu"}
    ::::field-group{.my-0}
      :::::field{name="analytics" type="boolean"}
      Par défaut à `false` - Active l'analytics pour votre projet (bientôt disponible).
      :::::
    
      :::::field{name="blob" type="boolean"}
      Par défaut à `false` - Active le stockage blob pour stocker des assets statiques, comme des images, vidéos et plus.
      :::::
    
      :::::field{name="cache" type="boolean"}
      Par défaut à `false` - Active le cache pour mettre en cache les réponses de vos routes serveur ou fonctions avec `cachedEventHandler` et `cachedFunction` de Nitro.
      :::::
    
      :::::field{name="database" type="boolean"}
      Par défaut à `false` - Active la base de données SQL pour stocker les données de votre application.
      :::::
    ::::
  :::

  :::tabs-item{icon="i-lucide-code" label="Code"}
  ```mdc
  ::field-group
    ::field{name="analytics" type="boolean"}
      Par défaut à `false` - Active l'analytics pour votre projet (bientôt disponible).
    ::
  
    ::field{name="blob" type="boolean"}
      Par défaut à `false` - Active le stockage blob pour stocker des assets statiques, comme des images, vidéos et plus.
    ::
  
    ::field{name="cache" type="boolean"}
      Par défaut à `false` - Active le cache pour mettre en cache les réponses de vos routes serveur ou fonctions avec `cachedEventHandler` et `cachedFunction` de Nitro.
    ::
  
    ::field{name="database" type="boolean"}
      Par défaut à `false` - Active la base de données SQL pour stocker les données de votre application.
    ::
  ::
  ```
  :::
::

### `Icon`

Utilisez le composant `icon` pour afficher une [Icône](https://ui.nuxt.com/components/icon) dans votre contenu.

::code-preview
:icon{name="i-simple-icons-nuxtdotjs"}

 

#code
```mdc
:icon{name="i-simple-icons-nuxtdotjs"}
```
::

### `Kbd`

Utilisez le composant `kbd` pour afficher un [Kbd](https://ui.nuxt.com/components/kbd) dans votre contenu.

::code-preview
#code
```mdc
:kbd{value="meta"} :kbd{value="K"}
```
::

### `Tabs`

Utilisez les composants `tabs` et `tabs-item` pour afficher des [Onglets](https://ui.nuxt.com/components/tabs) dans votre contenu.

::code-preview
  :::tabs{.w-full}
    ::::tabs-item{icon="i-lucide-code" label="Code"}
    ```mdc
    ::callout
    Lorem velit voluptate ex reprehenderit ullamco et culpa.
    ::
    ```
    ::::
  
    ::::tabs-item{icon="i-lucide-eye" label="Aperçu"}
      :::::callout
      Lorem velit voluptate ex reprehenderit ullamco et culpa.
      :::::
    ::::
  :::

#code
````mdc
::tabs{.w-full}
  :::tabs-item{icon="i-lucide-code" label="Code"}
    ```mdc
    ::::callout
    Lorem velit voluptate ex reprehenderit ullamco et culpa.
    ::::
    ```
  ::::

  :::tabs-item{icon="i-lucide-eye" label="Aperçu"}
    :::::callout
    Lorem velit voluptate ex reprehenderit ullamco et culpa.
    :::::
  :::
::
````
::

### `Steps`

Enveloppez vos titres avec le composant Steps pour afficher une liste d'étapes.

Utilisez la prop `level` pour définir quel titre sera utilisé pour les étapes.

::tabs
  :::tabs-item{.my-5 icon="i-lucide-eye" label="Aperçu"}
    ::::steps{level="4"}
    #### Démarrer un nouveau projet
    
    ```bash [Terminal]
    npx nuxi init -t github:nuxt-ui-pro/docus
    ```
    
    #### Lancer le CLI docus pour démarrer votre serveur de dev
    
    ```bash [Terminal]
    docus dev
    ```
    ::::
  :::

  :::tabs-item{icon="i-lucide-code" label="Code"}
  ````mdc
  ::steps{level="4"}
    #### Démarrer un nouveau projet
    
    ```bash [Terminal]
    npx nuxi init -t github:nuxt-ui-pro/docus
    ```
    
    #### Lancer le CLI docus pour démarrer votre serveur de dev
    
    ```bash [Terminal]
    docus dev
    ```
  ::
  ````
  :::
::
